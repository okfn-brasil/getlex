
/*-------------------------------------------------------------------
 * Estrutura de um nome uniforme (URN) do espaço "lex"
 *------------------------------------------------------------------- */

URN ::= "urn:" NID ":" NSS-lex 
NID ::= "lex"


/*-------------------------------------------------------------------
 * Estrutura de um nome uniforme do espaço "lex" internacionalizado
 *------------------------------------------------------------------- */

NSS-lex ::= local ":" NSS-lex-local 
local   ::= codigo-pais detalhamento-local?


/*-------------------------------------------------------------------
 * Estrutura do elemento local para o Brasil
 *------------------------------------------------------------------- */

codigo-pais        ::= "br"

detalhamento-local ::=  
           ( (";" unidade-federacao   (";" municipio)? )? ) 
         | ( (";" unidade-federacao)? (";" local-judiciario)? ) 

detalhe-ramo  ::= (  (("regiao." |"zona.") numeral+ ) 
             | ( "secao.judiciaria" | "comarca" ) 
             | ( unidade-federacao ("," unidade-federacao)* ) 
             | ( municipio ("," municipio)* )  ) 

unidade-federacao  ::= (minuscula | ".")+ 
municipio          ::= (minuscula | ".")+ 
local-judiciario ::= ramo-justica (";" detalhe-ramo)*  

ramo-justica  ::= "justica."  
   ("federal" | "trabalho" | "eleitoral" | "militar" | "estadual")


/*-------------------------------------------------------------------
 * Estrutura de um nome específico do "lex-br"
 *------------------------------------------------------------------- */

NSS-lex-local ::= documentoComplexo    
                | ( documentoIndividual ("!" fragmento)? )

documentoIndividual ::= documentoComplexo ("@" versao)? ("~" forma)? 
documentoComplexo   ::= documento 


/*-------------------------------------------------------------------
 * Estrutura do elemento documento
 *------------------------------------------------------------------- */

documento ::= autoridade ":" tipo-documento ":" descritor

/*-------------------------------------------------------------------
 * Estrutura do elemento autoridade
 *------------------------------------------------------------------- */

autoridade     ::= sujeito ("," sujeito)* 
sujeito        ::=  ( instituicao  (";"  orgao)*  (";"  funcao)? )  |  cargo 

instituicao ::= normal+ 
orgao       ::= normal+ 
funcao      ::= normal+ 
cargo       ::= normal+


/*-------------------------------------------------------------------
 * Estrutura do elemento tipo-documento
 *------------------------------------------------------------------- */

tipo-documento ::=  ( 
     (tipo-norma | tipo-jurisprudencia | tipo-projeto-norma) 
     (";" nome-subtipo-sequenciador)? 
   ) | ( 
    "publicacao.oficial;" nome-periodico-oficial 
    (";" nome-secao-periodico-oficial)?  
    (";" detalhe-extra-suplemento)?  
  )

tipo-projeto-norma   ::= normal+ 
tipo-norma           ::= normal+ 
tipo-jurisprudencia  ::= normal+ 
nome-subtipo-sequenciador    ::= normal+ 
nome-periodico-oficial       ::= normal+ 
nome-secao-periodico-oficial ::= normal+

detalhe-extra-suplemento ::=  ( "edicao.extra" | "suplemento" ) ("." numeral+)?


/*------------------------------------------------------------------- */
/* Estrutura do elemento descritor */
/*------------------------------------------------------------------- */

descritor ::=  datasOuAno ";" identificadores  
                    ( ";" id-componente ("," titulo-componente)? )*  
                     (";retificacao." seq-retificacao)?   
identificadores ::= ( id-documento  ("," id-documento)* ) 
                    | ( numero-lex ) 
                    | ( numero-seq )

id-documento      ::= normalID+  
numero-lex        ::= "lex-" numeral+ 
numero-seq        ::= "seq-" sigla-orgao "-" numeral+ 
sigla-orgao        ::= normal+ 
id-componente     ::= normal+ 
titulo-componente ::= normal+ 
seq-retificacao   ::= numeral+ 
datasOuAno        ::= datas | ano


/*-------------------------------------------------------------------
 * Estrutura do elemento fragmento
 *------------------------------------------------------------------- */
    
fragmento ::= ( id-particao | intervalo-ids ) ("," fragmento)* 
intervalo-ids ::= "[" id-particao "," id-particao "]" 
id-particao ::= normalID+


/*------------------------------------------------------------------- */
/* Estrutura do elemento versao  */
/*------------------------------------------------------------------- */

versao ::= [ ( datas  
               | "versao.original"  
               | "inicio.vigencia" 
               | "multivigente" ) 
             [ ";" evento [ ";" visao )? )? 
           )?  
evento ::= normal+
visao  ::= datas


/*------------------------------------------------------------------- */
/* Estrutura do elemento forma */
/*------------------------------------------------------------------- */
forma :: = tipo-forma [ ";" lingua )? ("," lingua)* )? 
tipo-forma  ::= normal+


lingua      ::= lingua-codigo  
                    ["-" lingua-script )?  ["-" lingua-regiao )?  
lingua-codigo ::= minuscula+  
lingua-script ::= minuscula+
lingua-regiao ::= minuscula+


/*------------------------------------------------------------------- */
/* Estrutura do elemento datas */
/*------------------------------------------------------------------- */
datas   ::= ( data ("," data)* ) | periodo   
periodo ::= "[" data "," data "]"              
data ::= ano "-" mes "-" dia 
ano ::= 4numeral 
mes ::= 2numeral 
dia ::= 2numeral


/*------------------------------------------------------------------- */
/* Caracteres admitidos */
/*------------------------------------------------------------------- */

aceitos-lex ::= normal | reservado | detalhaID | outro  
normal      ::= minuscula | numeral | "." 
normalID    ::= normal | detalhaID 

minuscula   ::=   "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" |  
              "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" |  
              "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" 

numeral     ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" 

reservado   ::= ":" | "@" | ";" | "," | "[" | "]" |  "!" | "~" 

detalhaID   ::= "_" | "-" 

outro       ::= "=" | "$"  

